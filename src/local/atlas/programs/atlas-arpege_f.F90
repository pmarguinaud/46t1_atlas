


USE ATLAS_MODULE, ONLY : ATLAS_LIBRARY, ATLAS_LOG,                                   &
                       & ATLAS_GRID, ATLAS_MESH, ATLAS_MESHGENERATOR,                &
                       & ATLAS_GRIDDISTRIBUTION, ATLAS_FUNCTIONSPACE_NODECOLUMNS,    &
                       & ATLAS_FIELD, ATLAS_CONFIG, ATLAS_FIELDSET, ATLAS_METADATA,  &
                       & ATLAS_STRUCTUREDGRID

USE FCKIT_MODULE, ONLY : FCKIT_MPI_COMM

USE FA_MOD, ONLY : FA_COM_DEFAULT, NEW_FA_DEFAULT, FACADR


USE MODD_IO_SURF_ARO,  ONLY : SURFEX_FIELD_BUF_CACHE,      &    
                            & SURFEX_FIELD_BUF_ADD,        &
                            & SURFEX_FIELD_BUF_WRITE_MISC, &
                            & SURFEX_FIELD_BUF_DEALLOC


USE PARKIND1, ONLY : JPRB, JPIM

IMPLICIT NONE

#include "abor1.intfb.h"

REAL (KIND=JPRB), PARAMETER :: RPI = 2.0_JPRB * ASIN (1.0_JPRB), &
                             & R2PI = 2._JPRB * RPI,             &
                             & RAD2DEG = 180._JPRB / RPI,        &
                             & DEG2RAD = RPI / 180._JPRB


CHARACTER (LEN=16), PARAMETER :: CLPREF (*) = &
[ 'SURF            ', 'PROF            ', 'PROF            ', &
  'SURF            ', 'SURF            ', 'SURF            ', &
  'SURF            ', 'SURF            ', 'SURF            ', &
  'SURF            ', 'SURF            ', 'SURF            ', &
  'SURF            ', 'SURF            ', 'SURF            ']

INTEGER (KIND=JPIM), PARAMETER :: INIVAU (*) = &
[ 0_JPIM            , 0_JPIM            , 0_JPIM            , &
  0_JPIM            , 0_JPIM            , 0_JPIM            , &
  0_JPIM            , 0_JPIM            , 0_JPIM            , &
  0_JPIM            , 0_JPIM            , 0_JPIM            , &
  0_JPIM            , 0_JPIM            , 0_JPIM            ]

CHARACTER (LEN=16), PARAMETER :: CLSUFF (*) = &
[ 'TEMPERATURE     ', 'TEMPERATURE     ', 'RESERV.EAU      ', &
  'RESERV.NEIGE    ', 'RESERV.EAU      ', 'Z0.FOIS.G       ', &
  'ALBEDO          ', 'EMISSIVITE      ', 'ET.GEOPOTENT    ', &
  'IND.TERREMER    ', 'PROP.VEGETAT    ', 'VAR.GEOP.ANI    ', &
  'VAR.GEOP.DIR    ', 'IND.VEG.DOMI    ', 'RESI.STO.MIN    ']



TYPE (ATLAS_STRUCTUREDGRID)            :: YLGRID
TYPE (ATLAS_MESH)                      :: YLMESH
TYPE (ATLAS_CONFIG)                    :: YLCONF
TYPE (ATLAS_MESHGENERATOR)             :: YLMESHGEN
TYPE (ATLAS_GRIDDISTRIBUTION)          :: YLDIST
TYPE (ATLAS_FUNCTIONSPACE_NODECOLUMNS) :: YLNDCO
TYPE (ATLAS_FIELDSET)                  :: YLFLSL
TYPE (FCKIT_MPI_COMM)                  :: YLCOMM



INTEGER :: IX, IY, I, J

INTEGER :: IRANK

CHARACTER (LEN=64) :: CLFILENAME

CALL GETARG (1, CLFILENAME)

CALL ATLAS_LIBRARY%INITIALISE()

YLCOMM = FCKIT_MPI_COMM ()
IRANK = YLCOMM%RANK ()

WRITE (0, *) __FILE__, ':', __LINE__, " IRANK = ", IRANK

CALL GFA (CLFILENAME, YLGRID)

PRINT *, " SIZE = ", YLGRID%SIZE ()

DO IY = 1, INT (YLGRID%NY (), JPIM)
  DO IX = 1, INT (YLGRID%NX (IY), JPIM)
    WRITE (*, '(2F20.10)') YLGRID%LONLAT (IX, IY)
  ENDDO
ENDDO

YLMESHGEN = ATLAS_MESHGENERATOR ()

YLCONF    = ATLAS_CONFIG()
!CALL YLCONF%SET ('type', 'equal_regions')
CALL YLCONF%SET ('type', 'checkerboard')
YLDIST    = ATLAS_GRIDDISTRIBUTION (YLGRID, YLCONF)
CALL YLCONF%FINAL ()

YLMESH    = YLMESHGEN%GENERATE (YLGRID, YLDIST)
YLNDCO    = ATLAS_FUNCTIONSPACE_NODECOLUMNS (YLMESH)
YLFLSL    = ATLAS_FIELDSET ()

BLOCK
  INTEGER (KIND=JPIM), PARAMETER :: INFLD = 3
  CALL RFA (CLFILENAME, YLGRID, YLNDCO, CLPREF (1:INFLD), &
          & INIVAU (1:INFLD), CLSUFF (1:INFLD), YLFLSL)
ENDBLOCK


PRINT *, " YLFLSL%SIZE = ", YLFLSL%SIZE ()

BLOCK
  REAL (KIND=JPRB), POINTER :: ZL (:)
  TYPE (ATLAS_FIELD) :: YLFLD

  DO J = 1, YLFLSL%SIZE ()
    YLFLD = YLFLSL%FIELD (J)

    ZL => NULL ()
    CALL YLFLD%DATA (ZL)

    WRITE (*, *) J, YLFLD%NAME (), ASSOCIATED (ZL), SUM (ZL) / SIZE (ZL)

    CALL YLFLD%FINAL ()
  ENDDO
ENDBLOCK


CALL YLFLSL%FINAL ()

CALL YLDIST%FINAL ()
CALL YLMESH%FINAL ()

CALL YLMESHGEN%FINAL ()
CALL YLGRID%FINAL ()


CALL ATLAS_LIBRARY%FINALISE()

CONTAINS

SUBROUTINE RFA (CDFILENAME, YDGRID, YDNDCO, CDPREF, KNIVAU, CDSUFF, YDFLSL)

USE FCKIT_MODULE, ONLY : FCKIT_MPI_COMM

CHARACTER (LEN=*),     INTENT (IN) :: CDFILENAME
CLASS (ATLAS_STRUCTUREDGRID),           INTENT (IN) :: YDGRID
TYPE (ATLAS_FUNCTIONSPACE_NODECOLUMNS), INTENT (IN) :: YDNDCO
CHARACTER (LEN=*),     INTENT (IN) :: CDPREF (:)
INTEGER (KIND=JPIM),   INTENT (IN) :: KNIVAU (:)
CHARACTER (LEN=*),     INTENT (IN) :: CDSUFF (:)
TYPE (ATLAS_FIELDSET), INTENT (INOUT) :: YDFLSL

#include "facilo.h"

CHARACTER (LEN=*), PARAMETER :: CLNOMC = 'c'
INTEGER (KIND=JPIM) :: ILUN, INBARP, INBARI, ILNOMA, IREP
INTEGER (KIND=JPIM) :: MYPROC, NPROC, IPROC
INTEGER (KIND=JPIM) :: ISIZEG
INTEGER (KIND=JPIM) :: JFLD1, JFLD2, INFLD, JFLD
INTEGER (KIND=JPIM) :: IFROM (SIZE (CDPREF))
TYPE (ATLAS_FIELD)  :: YLFLDL (SIZE (CDPREF))
TYPE (ATLAS_FIELD)  :: YLFLDG (SIZE (CDPREF))
TYPE (ATLAS_METADATA) :: YLMETA
TYPE (ATLAS_FIELDSET) :: YLFLSG
CHARACTER (LEN=16)  :: CLNOMA

YLCOMM = FCKIT_MPI_COMM ()
MYPROC = YLCOMM%RANK () + 1
NPROC  = YLCOMM%SIZE ()
INFLD  = SIZE (CDPREF)

YDFLSL    = ATLAS_FIELDSET ()
YLFLSG    = ATLAS_FIELDSET ()

DO IPROC = 1, NPROC
  JFLD1 = 1 + ((IPROC-1) * INFLD) / NPROC
  JFLD2 = 0 + ((IPROC+0) * INFLD) / NPROC
  IFROM (JFLD1:JFLD2) = IPROC
ENDDO

ILUN = 77_JPIM
INBARP = 0
INBARI = 0
CALL FAITOU (IREP, ILUN, .TRUE., TRIM (CLFILENAME), 'OLD', &
           & .TRUE., .TRUE., 2_JPIM, INBARP, INBARI, CLNOMC)


DO JFLD = 1, INFLD
  CALL FANFAN (IREP, ILUN, CDPREF (JFLD), KNIVAU (JFLD), CDSUFF (JFLD), CLNOMA, ILNOMA)

! Global field; zero size if this proc does not read the field
  ISIZEG = 0
  IF (IFROM (JFLD) == MYPROC) ISIZEG = INT (YDGRID%SIZE (), JPIM)
  YLFLDG (JFLD) = ATLAS_FIELD (NAME=CLNOMA, KIND=JPRB, SHAPE=[ISIZEG])
  YLMETA = YLFLDG (JFLD)%METADATA ()
! Set owner
  CALL YLMETA%SET ("owner", IFROM (JFLD)-1)
  CALL YLMETA%FINAL ()
  CALL YLFLSG%ADD (YLFLDG (JFLD))

! Distributed field : created by space function
  YLFLDL (JFLD) = YLNDCO%CREATE_FIELD (NAME=CLNOMA, KIND=JPRB)
  CALL YDFLSL%ADD (YLFLDL (JFLD))
ENDDO

BLOCK
  REAL (KIND=JPRB), POINTER :: ZFLDG (:)
  DO JFLD = 1, INFLD
    IF (IFROM (JFLD) == MYPROC) THEN
      CALL YLFLDG (JFLD)%DATA (ZFLDG)
      CALL FACILO (IREP, ILUN, CDPREF (JFLD), KNIVAU (JFLD), &
                 & CDSUFF (JFLD), ZFLDG, .FALSE.)
    ENDIF
  ENDDO
ENDBLOCK

CALL YLNDCO%SCATTER (YLFLSG, YDFLSL)

CALL FAIRME (IREP, ILUN, 'KEEP')

DO JFLD = 1, INFLD
  CALL YLFLDG (JFLD)%FINAL ()
  CALL YLFLDL (JFLD)%FINAL ()
ENDDO

CALL YLFLSG%FINAL ()

END SUBROUTINE

SUBROUTINE GFA (CDFILENAME, YDGRID)

USE ATLAS_MODULE, ONLY : ATLAS_REDUCEDGAUSSIANGRID, ATLAS_LAMBERTREGIONALGRID

CHARACTER (LEN=*), INTENT (IN) :: CDFILENAME
CLASS (ATLAS_STRUCTUREDGRID) :: YDGRID

CHARACTER (LEN=*), PARAMETER :: CLNOMC = 'c'
INTEGER (KIND=JPIM) :: ILUN, IREP, INBARP, INBARI, IRANGC
TYPE (FACADR), POINTER :: YLCADR

ILUN = 77_JPIM
INBARP = 0
INBARI = 0
CALL FAITOU (IREP, ILUN, .TRUE., TRIM (CDFILENAME), 'OLD', &
           & .TRUE., .TRUE., 2_JPIM, INBARP, INBARI, CLNOMC)

CALL FANUCA (CLNOMC, IRANGC, .FALSE.)
YLCADR => FA_COM_DEFAULT%CADRE (IRANGC)

IF (YLCADR%NTYPTR .LE. 0) THEN
BLOCK ! Regional model
  REAL (KIND=JPRB) :: DXINMETRES, DYINMETRES
  REAL (KIND=JPRB) :: LOVINDEGREES, LADINDEGREES, LATIN1INDEGREES, LATIN2INDEGREES
  INTEGER (KIND=JPIM) :: NUX, NUY

  DXINMETRES = YLCADR%SINLAT (7)
  DYINMETRES = YLCADR%SINLAT (8)
  NUX = YLCADR%NLOPAR (4)
  NUY = YLCADR%NLOPAR (6)

  LADINDEGREES    = YLCADR%SINLAT (4) * RAD2DEG
  LATIN1INDEGREES = YLCADR%SINLAT (4) * RAD2DEG
  LATIN2INDEGREES = YLCADR%SINLAT (4) * RAD2DEG
  LOVINDEGREES    = YLCADR%SINLAT (3) * RAD2DEG

  YDGRID = ATLAS_LAMBERTREGIONALGRID (YLCADR%NXLOPA, YLCADR%NLATIT, -NUX / 2 * DXINMETRES, -NUY / 2 * DYINMETRES, &
                                    & DXINMETRES, DYINMETRES, LOVINDEGREES, LADINDEGREES, LATIN1INDEGREES, LATIN2INDEGREES)

ENDBLOCK
ELSE 
BLOCK ! Global model
  INTEGER (KIND=JPIM), ALLOCATABLE :: NLOENG (:)
  REAL (KIND=JPRB) :: ZLONC, ZLATC

  ALLOCATE (NLOENG (YLCADR%NLATIT))

  IF (MODULO (YLCADR%NLATIT, 2) == 0) THEN
    NLOENG (1:YLCADR%NLATIT/2) = INT (YLCADR%NLOPAR (1:YLCADR%NLATIT/2), JPIM)
    NLOENG (YLCADR%NLATIT/2+1:YLCADR%NLATIT) = INT (NLOENG (YLCADR%NLATIT/2:1:-1), JPIM)
  ELSE
    CALL ABOR1 ('UNEXPECTED ODD NUMBER OF LATITUDES')
  ENDIF

  ZLATC = RAD2DEG * ASIN (YLCADR%SSLAPO)
  ZLONC = RAD2DEG * ATAN2 (YLCADR%SSLOPO, YLCADR%SCLOPO)

  YDGRID = ATLAS_REDUCEDGAUSSIANGRID (NLOENG, [ZLONC, ZLATC], YLCADR%SCODIL)

ENDBLOCK
ENDIF

CALL FAIRME (IREP, ILUN, 'KEEP')

END SUBROUTINE

END 

