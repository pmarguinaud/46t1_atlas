
USE ATLAS_MODULE, ONLY : ATLAS_LIBRARY, ATLAS_LOG, ATLAS_REDUCEDGAUSSIANGRID,     &
                       & ATLAS_GRID, ATLAS_MESH, ATLAS_MESHGENERATOR,             &
                       & ATLAS_GRIDDISTRIBUTION, ATLAS_FUNCTIONSPACE_NODECOLUMNS, &
                       & ATLAS_FIELD, ATLAS_CONFIG, ATLAS_FIELDSET, ATLAS_METADATA

USE FCKIT_MODULE, ONLY : FCKIT_MPI_COMM

USE FA_MOD, ONLY : FA_COM_DEFAULT, NEW_FA_DEFAULT, FACADR

USE PARKIND1, ONLY : JPRB, JPIM

IMPLICIT NONE

#include "abor1.intfb.h"

REAL (KIND=JPRB), PARAMETER :: RPI = 2.0_JPRB * ASIN (1.0_JPRB), &
                             & R2PI = 2._JPRB * RPI,             &
                             & RAD2DEG = 180._JPRB / RPI,        &
                             & DEG2RAD = RPI / 180._JPRB


CHARACTER (LEN=*), PARAMETER :: CLNOMC = 'c'
INTEGER (KIND=JPIM) :: ILUN, IREP, INBARP, INBARI

TYPE (ATLAS_REDUCEDGAUSSIANGRID)       :: YLGRID
TYPE (ATLAS_MESH)                      :: YLMESH
TYPE (ATLAS_MESHGENERATOR)             :: YLMESHGEN
TYPE (ATLAS_GRIDDISTRIBUTION)          :: YLDIST
TYPE (ATLAS_FUNCTIONSPACE_NODECOLUMNS) :: YLNDCO
TYPE (ATLAS_FIELD), DIMENSION (2)      :: YLFLDL, YLFLDG
TYPE (ATLAS_CONFIG)                    :: YLCONF
TYPE (ATLAS_FIELDSET)                  :: YLFLSL, YLFLSG
TYPE (ATLAS_METADATA)                  :: YLMETA
TYPE (FCKIT_MPI_COMM)                  :: YLCOMM


REAL (KIND=JPRB), POINTER :: ZL (:)
REAL (KIND=JPRB), POINTER :: ZG (:)

INTEGER :: IX, IY, I, J

INTEGER :: IRANK

CHARACTER (LEN=64) :: CLFILENAME

CALL GETARG (1, CLFILENAME)

CALL ATLAS_LIBRARY%INITIALISE()

YLCOMM = FCKIT_MPI_COMM ()
IRANK = YLCOMM%RANK ()

WRITE (0, *) __FILE__, ':', __LINE__, " IRANK = ", IRANK


ILUN = 77_JPIM
INBARP = 0
INBARI = 0
CALL FAITOU (IREP, ILUN, .TRUE., TRIM (CLFILENAME), 'OLD', &
           & .TRUE., .TRUE., 2_JPIM, INBARP, INBARI, CLNOMC)

BLOCK
  INTEGER (KIND=JPIM) :: IRANGC
  INTEGER (KIND=JPIM), ALLOCATABLE :: NLOENG (:)
  TYPE (FACADR), POINTER :: YLCADR
  REAL (KIND=JPRB) :: ZLONC, ZLATC

  CALL FANUCA (CLNOMC, IRANGC, .FALSE.)

  YLCADR => FA_COM_DEFAULT%CADRE (IRANGC)

  ALLOCATE (NLOENG (YLCADR%NLATIT))

  IF (MODULO (YLCADR%NLATIT, 2) == 0) THEN
    NLOENG (1:YLCADR%NLATIT/2) = INT (YLCADR%NLOPAR (1:YLCADR%NLATIT/2), JPIM)
    NLOENG (YLCADR%NLATIT/2+1:YLCADR%NLATIT) = INT (NLOENG (YLCADR%NLATIT/2:1:-1), JPIM)
  ELSE
    CALL ABOR1 ('UNEXPECTED ODD NUMBER OF LATITUDES')
  ENDIF

  ZLATC = RAD2DEG * ASIN (YLCADR%SSLAPO)
  ZLONC = RAD2DEG * ATAN2 (YLCADR%SSLOPO, YLCADR%SCLOPO)

  YLGRID = ATLAS_REDUCEDGAUSSIANGRID (NLOENG, [ZLONC, ZLATC], YLCADR%SCODIL)

ENDBLOCK

CALL FAIRME (IREP, ILUN, 'KEEP')

PRINT *, " SIZE = ", YLGRID%SIZE ()

DO IY = 1, INT (YLGRID%NY (), JPIM)
  DO IX = 1, INT (YLGRID%NX (IY), JPIM)
    WRITE (*, '(2F20.10)') YLGRID%LONLAT (IX, IY)
  ENDDO
ENDDO

YLMESHGEN = ATLAS_MESHGENERATOR ()

YLCONF    = ATLAS_CONFIG()
CALL YLCONF%SET ('type', 'equal_regions')
YLDIST    = ATLAS_GRIDDISTRIBUTION (YLGRID, YLCONF)
CALL YLCONF%FINAL ()

YLMESH    = YLMESHGEN%GENERATE (YLGRID, YLDIST)

YLNDCO    = ATLAS_FUNCTIONSPACE_NODECOLUMNS (YLMESH)

YLFLSL    = ATLAS_FIELDSET ()
YLFLSG    = ATLAS_FIELDSET ()

DO I = 1, SIZE (YLFLDL)
  YLFLDL (I) = YLNDCO%CREATE_FIELD (NAME='FIELD1', KIND=JPRB)
  YLFLDG (I) = ATLAS_FIELD (NAME='FIELD1', KIND=JPRB, SHAPE=[YLGRID%SIZE ()])
  YLMETA = YLFLDG (I)%METADATA ()
  CALL YLMETA%SET ("owner", I-1)
  CALL YLMETA%FINAL ()
  CALL YLFLSL%ADD (YLFLDL (I))
  CALL YLFLSG%ADD (YLFLDG (I))
ENDDO


DO I = 1, SIZE (YLFLDL)
  CALL YLFLDL (I)%DATA (ZL)

  ZL = 100 * (I-1) + REAL (IRANK, JPRB)

  WRITE (0, *) __FILE__, ':', __LINE__, SIZE (ZL)
ENDDO

DO I = 1, SIZE (YLFLDG)

  CALL YLFLDG (I)%DATA (ZG)
  ZG = -999._JPRB

ENDDO


CALL YLNDCO%GATHER (YLFLSL, YLFLSG)

DO I = 1, SIZE (YLFLDG)

  CALL YLFLDG (I)%DATA (ZG)

  WRITE (0, *) __FILE__, ':', __LINE__, " ZG "
  DO J = 1, SIZE (ZG)
    WRITE (0, '(I8,F12.4)') J, ZG (J)
  ENDDO
  WRITE (0, *) __FILE__, ':', __LINE__, "----"

ENDDO

DO I = 1, SIZE (YLFLDL)
  CALL YLFLDL (I)%FINAL ()
ENDDO

DO I = 1, SIZE (YLFLDG)
  CALL YLFLDG (I)%FINAL ()
ENDDO

CALL YLFLSG%FINAL ()
CALL YLFLSL%FINAL ()

CALL YLDIST%FINAL ()
CALL YLMESH%FINAL ()

CALL YLMESHGEN%FINAL ()
CALL YLGRID%FINAL ()

CALL ATLAS_LIBRARY%FINALISE()

END 

