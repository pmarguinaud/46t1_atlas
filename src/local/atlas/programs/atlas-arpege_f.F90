
USE ATLAS_MODULE, ONLY : ATLAS_LIBRARY, ATLAS_LOG, ATLAS_REDUCEDGAUSSIANGRID, &
                       & ATLAS_GRID

USE FA_MOD, ONLY : FA_COM_DEFAULT, NEW_FA_DEFAULT, FACADR

USE PARKIND1, ONLY : JPRB, JPIM

IMPLICIT NONE

#include "abor1.intfb.h"

REAL (KIND=JPRB), PARAMETER :: RPI = 2.0_JPRB * ASIN (1.0_JPRB), &
                             & R2PI = 2._JPRB * RPI,             &
                             & RAD2DEG = 180._JPRB / RPI,        &
                             & DEG2RAD = RPI / 180._JPRB


CHARACTER (LEN=*), PARAMETER :: CLNOMC = 'c'
INTEGER (KIND=JPIM) :: ILUN, IREP, INBARP, INBARI

TYPE (ATLAS_REDUCEDGAUSSIANGRID) :: GRID

INTEGER :: IX, IY

CHARACTER (LEN=64) :: CLFILENAME

CALL GETARG (1, CLFILENAME)

CALL ATLAS_LIBRARY%INITIALISE()


ILUN = 77_JPIM
INBARP = 0
INBARI = 0
CALL FAITOU (IREP, ILUN, .TRUE., TRIM (CLFILENAME), 'OLD', &
           & .TRUE., .TRUE., 2_JPIM, INBARP, INBARI, CLNOMC)

BLOCK
  INTEGER (KIND=JPIM) :: IRANGC
  INTEGER (KIND=JPIM), ALLOCATABLE :: NLOENG (:)
  TYPE (FACADR), POINTER :: YLCADR
  REAL (KIND=JPRB) :: ZLONC, ZLATC

  CALL FANUCA (CLNOMC, IRANGC, .FALSE.)

  YLCADR => FA_COM_DEFAULT%CADRE (IRANGC)

  ALLOCATE (NLOENG (YLCADR%NLATIT))

  IF (MODULO (YLCADR%NLATIT, 2) == 0) THEN
    NLOENG (1:YLCADR%NLATIT/2) = YLCADR%NLOPAR (1:YLCADR%NLATIT/2)
    NLOENG (YLCADR%NLATIT/2+1:YLCADR%NLATIT) = NLOENG (YLCADR%NLATIT/2:1:-1)
  ELSE
    CALL ABOR1 ('UNEXPECTED ODD NUMBER OF LATITUDES')
  ENDIF

  ZLATC = RAD2DEG * ASIN (YLCADR%SSLAPO)
  ZLONC = RAD2DEG * ATAN2 (YLCADR%SSLOPO, YLCADR%SCLOPO)

  GRID = ATLAS_REDUCEDGAUSSIANGRID (NLOENG, [ZLONC, ZLATC], YLCADR%SCODIL)

ENDBLOCK

CALL FAIRME (IREP, ILUN, 'KEEP')

PRINT *, " SIZE = ", GRID%SIZE ()

DO IY = 1, GRID%NY ()
  DO IX = 1, GRID%NX (IY)
    WRITE (*, '(2F20.10)') GRID%LONLAT (IX, IY)
  ENDDO
ENDDO

CALL GRID%FINAL ()

CALL ATLAS_LIBRARY%FINALISE()

END 

