


USE ATLAS_MODULE, ONLY : ATLAS_LIBRARY, ATLAS_LOG,                                   &
                       & ATLAS_GRID, ATLAS_MESH, ATLAS_MESHGENERATOR,                &
                       & ATLAS_GRIDDISTRIBUTION, ATLAS_FUNCTIONSPACE_NODECOLUMNS,    &
                       & ATLAS_FIELD, ATLAS_CONFIG, ATLAS_FIELDSET, ATLAS_METADATA,  &
                       & ATLAS_STRUCTUREDGRID

USE FCKIT_MODULE, ONLY : FCKIT_MPI_COMM

USE FA_MOD, ONLY : FA_COM_DEFAULT, NEW_FA_DEFAULT, FACADR


USE MODD_IO_SURF_ARO,  ONLY : SURFEX_FIELD_BUF_CACHE,      &    
                            & SURFEX_FIELD_BUF_ADD,        &
                            & SURFEX_FIELD_BUF_WRITE_MISC, &
                            & SURFEX_FIELD_BUF_DEALLOC


USE PARKIND1, ONLY : JPRB, JPIM

IMPLICIT NONE

#include "abor1.intfb.h"

REAL (KIND=JPRB), PARAMETER :: RPI = 2.0_JPRB * ASIN (1.0_JPRB), &
                             & R2PI = 2._JPRB * RPI,             &
                             & RAD2DEG = 180._JPRB / RPI,        &
                             & DEG2RAD = RPI / 180._JPRB


CHARACTER (LEN=16), PARAMETER :: CLPREF (*) = &
[ 'SURF            ', 'PROF            ', 'PROF            ', &
  'SURF            ', 'SURF            ', 'SURF            ', &
  'SURF            ', 'SURF            ', 'SURF            ', &
  'SURF            ', 'SURF            ', 'SURF            ', &
  'SURF            ', 'SURF            ', 'SURF            ']

INTEGER (KIND=JPIM), PARAMETER :: INIVAU (*) = &
[ 0_JPIM            , 0_JPIM            , 0_JPIM            , &
  0_JPIM            , 0_JPIM            , 0_JPIM            , &
  0_JPIM            , 0_JPIM            , 0_JPIM            , &
  0_JPIM            , 0_JPIM            , 0_JPIM            , &
  0_JPIM            , 0_JPIM            , 0_JPIM            ]

CHARACTER (LEN=16), PARAMETER :: CLSUFF (*) = &
[ 'TEMPERATURE     ', 'TEMPERATURE     ', 'RESERV.EAU      ', &
  'RESERV.NEIGE    ', 'RESERV.EAU      ', 'Z0.FOIS.G       ', &
  'ALBEDO          ', 'EMISSIVITE      ', 'ET.GEOPOTENT    ', &
  'IND.TERREMER    ', 'PROP.VEGETAT    ', 'VAR.GEOP.ANI    ', &
  'VAR.GEOP.DIR    ', 'IND.VEG.DOMI    ', 'RESI.STO.MIN    ']

INTEGER (KIND=JPIM), PARAMETER :: INFLD = 10


TYPE (ATLAS_STRUCTUREDGRID)            :: YLGRID
TYPE (ATLAS_MESH)                      :: YLMESH
TYPE (ATLAS_CONFIG)                    :: YLCONF
TYPE (ATLAS_MESHGENERATOR)             :: YLMESHGEN
TYPE (ATLAS_GRIDDISTRIBUTION)          :: YLDIST
TYPE (ATLAS_FUNCTIONSPACE_NODECOLUMNS) :: YLNDCO
TYPE (ATLAS_FIELDSET)                  :: YLFLSL
TYPE (FCKIT_MPI_COMM)                  :: YLCOMM



INTEGER :: IX, IY, I, J

INTEGER :: IRANK

CHARACTER (LEN=64) :: CLFILENAME

CALL GETARG (1, CLFILENAME)

CALL ATLAS_LIBRARY%INITIALISE()

YLCOMM = FCKIT_MPI_COMM ()
IRANK = YLCOMM%RANK ()

WRITE (*, *) __FILE__, ':', __LINE__, " IRANK = ", IRANK

CALL GFA (CLFILENAME, YLGRID)

PRINT *, " SIZE = ", YLGRID%SIZE ()

DO IY = 1, INT (YLGRID%NY (), JPIM)
  DO IX = 1, INT (YLGRID%NX (IY), JPIM)
    WRITE (*, '(2F20.10)') YLGRID%LONLAT (IX, IY)
  ENDDO
ENDDO

YLMESHGEN = ATLAS_MESHGENERATOR ()

YLCONF    = ATLAS_CONFIG()
CALL YLCONF%SET ('type', 'equal_regions')
!CALL YLCONF%SET ('type', 'checkerboard')
YLDIST    = ATLAS_GRIDDISTRIBUTION (YLGRID, YLCONF)
CALL YLCONF%FINAL ()

PRINT *, " YLDIST%NB_PARTITIONS () = ", YLDIST%NB_PARTITIONS ()

DO I = 1, INT (YLGRID%SIZE (), JPIM)
  WRITE (*, '(I6,I6)') I, YLDIST%PARTITION (I)
ENDDO

YLMESH    = YLMESHGEN%GENERATE (YLGRID, YLDIST)
YLNDCO    = ATLAS_FUNCTIONSPACE_NODECOLUMNS (YLMESH)
YLFLSL    = ATLAS_FIELDSET ()

CALL RFA (CLFILENAME, YLGRID, YLNDCO, CLPREF (1:INFLD), &
        & INIVAU (1:INFLD), CLSUFF (1:INFLD), YLFLSL)


PRINT *, " YLFLSL%SIZE = ", YLFLSL%SIZE ()

CALL WFA (TRIM (CLFILENAME)//'.out', YLGRID, YLNDCO, CLPREF (1:INFLD), &
        & INIVAU (1:INFLD), CLSUFF (1:INFLD), YLFLSL)

BLOCK
  REAL (KIND=JPRB), POINTER :: ZFLDL (:)
  TYPE (ATLAS_FIELD) :: YLFLD

  DO J = 1, YLFLSL%SIZE ()
    YLFLD = YLFLSL%FIELD (J)

    ZFLDL => NULL ()
    CALL YLFLD%DATA (ZFLDL)

    WRITE (*, '("LOCAL ",A16,I16,E12.4)') YLFLD%NAME (), SIZE (ZFLDL), SUM (ZFLDL) / SIZE (ZFLDL)

    CALL YLFLD%FINAL ()
  ENDDO
ENDBLOCK


CALL YLFLSL%FINAL ()

CALL YLDIST%FINAL ()
CALL YLMESH%FINAL ()

CALL YLMESHGEN%FINAL ()
CALL YLGRID%FINAL ()


CALL ATLAS_LIBRARY%FINALISE()

CONTAINS

SUBROUTINE OFA (KLUN, CDFILENAME, YDGRID)

INTEGER (KIND=JPIM),           INTENT (IN) :: KLUN
CHARACTER (LEN=*),             INTENT (IN) :: CDFILENAME
CLASS (ATLAS_STRUCTUREDGRID),  INTENT (IN) :: YDGRID

TYPE (ATLAS_CONFIG) :: YLCONF
TYPE (ATLAS_CONFIG) :: YLCFPR ! projection
TYPE (ATLAS_CONFIG) :: YLCFDO ! domain
TYPE (ATLAS_CONFIG) :: YLCFXS ! xspace
TYPE (ATLAS_CONFIG) :: YLCFYS ! yspace

CHARACTER (LEN=*), PARAMETER   :: CLNOMC = 'c'
CHARACTER (LEN=:), ALLOCATABLE :: CLPROJ
LOGICAL             :: LLFOUND


YLCONF = YDGRID%SPEC ()

PRINT *, YLCONF%JSON ()

LLFOUND = YLCONF%GET ("projection", YLCFPR)
LLFOUND = YLCONF%GET ("domain",     YLCFDO)
LLFOUND = YLCONF%GET ("xspace",     YLCFXS)
LLFOUND = YLCONF%GET ("yspace",     YLCFYS)

PRINT *, YLCFPR%JSON ()
PRINT *, YLCFDO%JSON ()
PRINT *, YLCFXS%JSON ()
PRINT *, YLCFYS%JSON ()

LLFOUND = YLCFPR%GET ("type",      CLPROJ)

SELECT CASE (TRIM (CLPROJ))
  CASE ("rotated_schmidt") 
  BLOCK
    REAL (KIND=JPRB) :: ZAHYBR (2), ZBHYBR (2), ZSLAPO, ZCLOPO, ZSLOPO, ZCODIL, ZREFER
    INTEGER (KIND=JPIM) :: INIVER, ITYPTR, ITRONC, INLATI, INXLON
    REAL (KIND=JPRB), ALLOCATABLE :: ZCENTRE (:), RMU (:)
    INTEGER (KIND=JPIM), ALLOCATABLE :: NLOENG (:), INOZPA (:)
    INTEGER (KIND=JPIM) :: NDGLG, IGLG
  
    LLFOUND = YLCFPR%GET ("north_pole",        ZCENTRE )
    LLFOUND = YLCFPR%GET ("stretching_factor", ZCODIL  )
    LLFOUND = YLCFXS%GET ("N[]",               NLOENG  )
    LLFOUND = YLCFYS%GET ("N",                 NDGLG   )
  
  
    INIVER = 1_JPIM
    ZAHYBR = 0._JPRB
    ZBHYBR = 0._JPRB
    ITYPTR = 2_JPIM
    ZSLAPO = SIN (ZCENTRE (2) * DEG2RAD)
    ZCLOPO = COS (ZCENTRE (1) * DEG2RAD)
    ZSLOPO = SIN (ZCENTRE (1) * DEG2RAD)
    ITRONC = NDGLG-1
    INLATI = NDGLG
    INXLON = MAXVAL (NLOENG)
    ALLOCATE (INOZPA (NDGLG))
    INOZPA = MIN (ITRONC, NLOENG/2_JPIM-1_JPIM)
    ZREFER = 1._JPRB

    ALLOCATE (RMU (NDGLG))

    DO IGLG = 1, NDGLG
      RMU (IGLG) = SIN (YDGRID%Y (IGLG) * DEG2RAD)
    ENDDO

    CALL FACADE (CLNOMC, ITYPTR, ZSLAPO, ZCLOPO, ZSLOPO, &
    &            ZCODIL, ITRONC, INLATI, INXLON, NLOENG, &
    &            INOZPA, RMU,    INIVER, ZREFER, ZAHYBR, &
    &            ZBHYBR, .FALSE.)

    
  ENDBLOCK
  CASE ("lambert_conformal_conic") 
  BLOCK
    REAL (KIND=JPRB) :: ZAHYBR (2), ZBHYBR (2), ZSLAPO, ZCLOPO, ZSLOPO, ZCODIL, ZREFER
    INTEGER (KIND=JPIM) :: INIVER, ITYPTR, ITRONC, INLATI, INXLON
    REAL (KIND=JPRB), ALLOCATABLE :: ZSINLA (:)
    INTEGER (KIND=JPIM) :: INLOPA (8)
    INTEGER (KIND=JPIM), ALLOCATABLE :: INOZPA (:)
    REAL (KIND=JPRB) :: XMIN, XMAX, YMIN, YMAX
    REAL (KIND=JPRB) :: DXINMETRES, DYINMETRES
    REAL (KIND=JPRB) :: LOVINDEGREES, LADINDEGREES, LATIN1INDEGREES, LATIN2INDEGREES
    INTEGER (KIND=JPIM) :: NUX, NUY, NX, NY
  
    LLFOUND = YLCFDO%GET ("latitude0",  LADINDEGREES   )
    LLFOUND = YLCFDO%GET ("longitude0", LOVINDEGREES   )
    LLFOUND = YLCFDO%GET ("latitude1",  LATIN1INDEGREES)
    LLFOUND = YLCFDO%GET ("latitude2",  LATIN2INDEGREES)
    LLFOUND = YLCFDO%GET ("xmin",       XMIN           )
    LLFOUND = YLCFDO%GET ("xmax",       XMAX           )
    LLFOUND = YLCFDO%GET ("ymin",       YMIN           )
    LLFOUND = YLCFDO%GET ("ymax",       YMAX           )
    LLFOUND = YLCFXS%GET ("N",          NX             )
    LLFOUND = YLCFYS%GET ("N",          NY             )
  
    DXINMETRES = (XMAX - XMIN) / REAL (NX - 1, JPRB)
    DYINMETRES = (YMAX - YMIN) / REAL (NY - 1, JPRB)
  
    NUX =  NINT (-2 * XMIN / DXINMETRES)
    NUY =  NINT (-2 * YMIN / DYINMETRES)
  
  
    WRITE (*, *) "latitude0",  LADINDEGREES   
    WRITE (*, *) "longitude0", LOVINDEGREES   
    WRITE (*, *) "latitude1",  LATIN1INDEGREES
    WRITE (*, *) "latitude2",  LATIN2INDEGREES
    WRITE (*, *) "xmin",       XMIN           
    WRITE (*, *) "xmax",       XMAX           
    WRITE (*, *) "ymin",       YMIN           
    WRITE (*, *) "ymax",       YMAX           
    WRITE (*, *) "NX",         NX            
    WRITE (*, *) "NY",         NY            
    WRITE (*, *) "DXINMETRES", DXINMETRES
    WRITE (*, *) "DYINMETRES", DYINMETRES
    WRITE (*, *) "NUX",        NUX            
    WRITE (*, *) "NUY",        NUY            
  
    INLATI = NY
    INXLON = NX
    ZSLAPO = 0._JPRB
    ZCLOPO = 0._JPRB
    ZSLOPO = 0._JPRB
    ZCODIL = 0._JPRB
    ZREFER = 0._JPRB
    INIVER = 0._JPRB
    ZAHYBR = 0._JPRB
    ZBHYBR = 0._JPRB
!   ITYPTR = 

  
  
  ENDBLOCK
END SELECT


BLOCK
  INTEGER (KIND=JPIM) :: INBARP, INBARI, IREP
  INBARP = 0; INBARI = 0
  CALL FAITOU (IREP, KLUN, .TRUE., TRIM (CDFILENAME), 'NEW', &
             & .TRUE., .TRUE., 2_JPIM, INBARP, INBARI, CLNOMC)
  CALL FACAGE (CLNOMC, .FALSE.)
  CALL FAUTIF (IREP, KLUN, 'ARP')
ENDBLOCK

BLOCK
  INTEGER (KIND=JPIM) :: IDATEF (11), IREP
  IDATEF = [1990,  2, 15, 12,  0,   1,  0, 24,   1, 0, 0]
  CALL FANDAR (IREP, KLUN, IDATEF)
ENDBLOCK

CALL YLCFPR%FINAL ()
CALL YLCFXS%FINAL ()
CALL YLCFYS%FINAL ()
CALL YLCFDO%FINAL ()
CALL YLCONF%FINAL ()

END SUBROUTINE

SUBROUTINE WFA (CDFILENAME, YDGRID, YDNDCO, CDPREF, KNIVAU, CDSUFF, YDFLSL)

USE FCKIT_MODULE, ONLY : FCKIT_MPI_COMM

CHARACTER (LEN=*),     INTENT (IN) :: CDFILENAME
CLASS (ATLAS_STRUCTUREDGRID),           INTENT (IN) :: YDGRID
TYPE (ATLAS_FUNCTIONSPACE_NODECOLUMNS), INTENT (IN) :: YDNDCO
CHARACTER (LEN=*),     INTENT (IN) :: CDPREF (:)
INTEGER (KIND=JPIM),   INTENT (IN) :: KNIVAU (:)
CHARACTER (LEN=*),     INTENT (IN) :: CDSUFF (:)
TYPE (ATLAS_FIELDSET), INTENT (IN) :: YDFLSL

#include "abor1.intfb.h"
#include "faieno.h"

INTEGER (KIND=JPIM) :: ITO (SIZE (CDPREF))
TYPE (ATLAS_FIELD)  :: YLFLDL (SIZE (CDPREF))
TYPE (ATLAS_FIELD)  :: YLFLDG (SIZE (CDPREF))
TYPE (ATLAS_FIELDSET) :: YLFLSG
INTEGER (KIND=JPIM) :: MYPROC, NPROC, IPROC
INTEGER (KIND=JPIM) :: JFLD1, JFLD2, INFLD, JFLD
TYPE (FCKIT_MPI_COMM) :: YLCOMM
CHARACTER (LEN=128) :: CLFILENAME, CLPROC
INTEGER (KIND=JPIM) :: ILUN, IREP

YLCOMM = FCKIT_MPI_COMM ()
MYPROC = YLCOMM%RANK () + 1
NPROC  = YLCOMM%SIZE ()
INFLD = SIZE (CDPREF)


DO IPROC = 1, NPROC
  JFLD1 = 1 + ((IPROC-1) * INFLD) / NPROC
  JFLD2 = 0 + ((IPROC+0) * INFLD) / NPROC
  ITO (JFLD1:JFLD2) = IPROC
ENDDO

WRITE (CLPROC, '(".",I4.4)') MYPROC
CLFILENAME = TRIM (CDFILENAME)//TRIM (CLPROC)

ILUN = 77

IF (ANY (ITO == MYPROC)) THEN
  CALL OFA (ILUN, CLFILENAME, YDGRID)
  BLOCK
    INTEGER (KIND=JPIM) :: INGRIB, INARG1, INARG2, INARG3, INARG4, INARG5
    CALL FAVEUR (IREP, ILUN, INGRIB, INARG1, INARG2, INARG3, INARG4, INARG5)
    INGRIB = 123_JPIM
    CALL FAGOTE (IREP, ILUN, INGRIB, INARG1, INARG2, INARG3, INARG4, INARG5)
  ENDBLOCK
ENDIF

YLFLSG = ATLAS_FIELDSET ()

DO JFLD = 1, INFLD
BLOCK
  CHARACTER (LEN=16)  :: CLNOMA
  INTEGER (KIND=JPIM) :: ISIZEG
  TYPE (ATLAS_METADATA) :: YLMETA

! Distributed field 
  YLFLDL (JFLD) = YDFLSL%FIELD (JFLD)

  CLNOMA = YLFLDL (JFLD)%NAME ()

! Global field; zero size if this proc does not write the field
  ISIZEG = 0
  IF (ITO (JFLD) == MYPROC) ISIZEG = INT (YDGRID%SIZE (), JPIM)
  YLFLDG (JFLD) = ATLAS_FIELD (NAME=CLNOMA, KIND=JPRB, SHAPE=[ISIZEG])
  YLMETA = YLFLDG (JFLD)%METADATA ()
! Set owner
  CALL YLMETA%SET ("owner", ITO (JFLD)-1)
  CALL YLMETA%FINAL ()
  CALL YLFLSG%ADD (YLFLDG (JFLD))

ENDBLOCK
ENDDO

CALL YLNDCO%GATHER (YDFLSL, YLFLSG)

DO JFLD = 1, INFLD
  IF (ITO (JFLD) == MYPROC) THEN 
  BLOCK
    REAL (KIND=JPRB), POINTER :: ZFLDG (:)
    CALL YLFLDG (JFLD)%DATA (ZFLDG)
    CALL FAIENO (IREP, ILUN, CDPREF (JFLD), KNIVAU (JFLD), CDSUFF (JFLD), ZFLDG, .FALSE.)
  ENDBLOCK
  ENDIF
ENDDO


IF (ANY (ITO == MYPROC)) THEN
  CALL FAIRME (IREP, ILUN, 'KEEP')
ENDIF

DO JFLD = 1, INFLD
  CALL YLFLDG (JFLD)%FINAL ()
  CALL YLFLDL (JFLD)%FINAL ()
ENDDO

CALL YLFLSG%FINAL ()

END SUBROUTINE

SUBROUTINE RFA (CDFILENAME, YDGRID, YDNDCO, CDPREF, KNIVAU, CDSUFF, YDFLSL)

USE FCKIT_MODULE, ONLY : FCKIT_MPI_COMM

CHARACTER (LEN=*),     INTENT (IN) :: CDFILENAME
CLASS (ATLAS_STRUCTUREDGRID),           INTENT (IN) :: YDGRID
TYPE (ATLAS_FUNCTIONSPACE_NODECOLUMNS), INTENT (IN) :: YDNDCO
CHARACTER (LEN=*),     INTENT (IN) :: CDPREF (:)
INTEGER (KIND=JPIM),   INTENT (IN) :: KNIVAU (:)
CHARACTER (LEN=*),     INTENT (IN) :: CDSUFF (:)
TYPE (ATLAS_FIELDSET), INTENT (INOUT) :: YDFLSL

#include "facilo.h"

CHARACTER (LEN=*), PARAMETER :: CLNOMC = 'c'
INTEGER (KIND=JPIM) :: ILUN, INBARP, INBARI, IREP
INTEGER (KIND=JPIM) :: MYPROC, NPROC, IPROC
INTEGER (KIND=JPIM) :: JFLD1, JFLD2, INFLD, JFLD
INTEGER (KIND=JPIM) :: IFROM (SIZE (CDPREF))
TYPE (ATLAS_FIELD)  :: YLFLDL (SIZE (CDPREF))
TYPE (ATLAS_FIELD)  :: YLFLDG (SIZE (CDPREF))
TYPE (ATLAS_FIELDSET) :: YLFLSG
TYPE (FCKIT_MPI_COMM) :: YLCOMM

YLCOMM = FCKIT_MPI_COMM ()
MYPROC = YLCOMM%RANK () + 1
NPROC  = YLCOMM%SIZE ()
INFLD  = SIZE (CDPREF)

YDFLSL = ATLAS_FIELDSET ()
YLFLSG = ATLAS_FIELDSET ()

DO IPROC = 1, NPROC
  JFLD1 = 1 + ((IPROC-1) * INFLD) / NPROC
  JFLD2 = 0 + ((IPROC+0) * INFLD) / NPROC
  IFROM (JFLD1:JFLD2) = IPROC
ENDDO

ILUN = 77_JPIM
INBARP = 0
INBARI = 0
CALL FAITOU (IREP, ILUN, .TRUE., TRIM (CLFILENAME), 'OLD', &
           & .TRUE., .TRUE., 2_JPIM, INBARP, INBARI, CLNOMC)


DO JFLD = 1, INFLD
BLOCK
  TYPE (ATLAS_METADATA) :: YLMETA
  REAL (KIND=JPRB), POINTER :: ZFLDL (:)
  CHARACTER (LEN=16)  :: CLNOMA
  INTEGER (KIND=JPIM) :: ISIZEG, ILNOMA

  CALL FANFAN (IREP, ILUN, CDPREF (JFLD), KNIVAU (JFLD), CDSUFF (JFLD), CLNOMA, ILNOMA)

! Global field; zero size if this proc does not read the field
  ISIZEG = 0
  IF (IFROM (JFLD) == MYPROC) ISIZEG = INT (YDGRID%SIZE (), JPIM)
  YLFLDG (JFLD) = ATLAS_FIELD (NAME=CLNOMA, KIND=JPRB, SHAPE=[ISIZEG])
  YLMETA = YLFLDG (JFLD)%METADATA ()
! Set owner
  CALL YLMETA%SET ("owner", IFROM (JFLD)-1)
  CALL YLMETA%FINAL ()
  CALL YLFLSG%ADD (YLFLDG (JFLD))

! Distributed field : created by space function
  YLFLDL (JFLD) = YLNDCO%CREATE_FIELD (NAME=CLNOMA, KIND=JPRB)
  CALL YLFLDL (JFLD)%DATA (ZFLDL)
  ZFLDL = 0._JPRB ! Not necessary

  CALL YDFLSL%ADD (YLFLDL (JFLD))
ENDBLOCK
ENDDO

DO JFLD = 1, INFLD
  IF (IFROM (JFLD) == MYPROC) THEN
  BLOCK
    REAL (KIND=JPRB), POINTER :: ZFLDG (:)
    CALL YLFLDG (JFLD)%DATA (ZFLDG)
    CALL FACILO (IREP, ILUN, CDPREF (JFLD), KNIVAU (JFLD), &
               & CDSUFF (JFLD), ZFLDG, .FALSE.)
    WRITE (*, '("GLOBAL",A16,I16,E12.4)') TRIM (CDPREF (JFLD))//TRIM (CDSUFF (JFLD)), SIZE (ZFLDG), SUM (ZFLDG) / SIZE (ZFLDG)
  ENDBLOCK
  ENDIF
ENDDO

CALL YLNDCO%SCATTER (YLFLSG, YDFLSL)

CALL FAIRME (IREP, ILUN, 'KEEP')

DO JFLD = 1, INFLD
  CALL YLFLDG (JFLD)%FINAL ()
  CALL YLFLDL (JFLD)%FINAL ()
ENDDO

CALL YLFLSG%FINAL ()

END SUBROUTINE

SUBROUTINE GFA (CDFILENAME, YDGRID)

USE ATLAS_MODULE, ONLY : ATLAS_REDUCEDGAUSSIANGRID, ATLAS_LAMBERTREGIONALGRID

CHARACTER (LEN=*), INTENT (IN) :: CDFILENAME
CLASS (ATLAS_STRUCTUREDGRID) :: YDGRID

CHARACTER (LEN=*), PARAMETER :: CLNOMC = 'c'
INTEGER (KIND=JPIM) :: ILUN, IREP, INBARP, INBARI, IRANGC
TYPE (FACADR), POINTER :: YLCADR

ILUN = 77_JPIM
INBARP = 0
INBARI = 0
CALL FAITOU (IREP, ILUN, .TRUE., TRIM (CDFILENAME), 'OLD', &
           & .TRUE., .TRUE., 2_JPIM, INBARP, INBARI, CLNOMC)

CALL FANUCA (CLNOMC, IRANGC, .FALSE.)
YLCADR => FA_COM_DEFAULT%CADRE (IRANGC)

IF (YLCADR%NTYPTR .LE. 0) THEN
BLOCK ! Regional model
  REAL (KIND=JPRB) :: DXINMETRES, DYINMETRES
  REAL (KIND=JPRB) :: LOVINDEGREES, LADINDEGREES, LATIN1INDEGREES, LATIN2INDEGREES
  INTEGER (KIND=JPIM) :: NUX, NUY

  DXINMETRES = YLCADR%SINLAT (7)
  DYINMETRES = YLCADR%SINLAT (8)
  NUX = INT (YLCADR%NLOPAR (4), JPIM)
  NUY = INT (YLCADR%NLOPAR (6), JPIM)

  LADINDEGREES    = YLCADR%SINLAT (4) * RAD2DEG
  LATIN1INDEGREES = YLCADR%SINLAT (4) * RAD2DEG
  LATIN2INDEGREES = YLCADR%SINLAT (4) * RAD2DEG
  LOVINDEGREES    = YLCADR%SINLAT (3) * RAD2DEG

  YDGRID = ATLAS_LAMBERTREGIONALGRID (YLCADR%NXLOPA, YLCADR%NLATIT, -NUX / 2 * DXINMETRES, -NUY / 2 * DYINMETRES, &
                                    & DXINMETRES, DYINMETRES, LOVINDEGREES, LADINDEGREES, LATIN1INDEGREES, LATIN2INDEGREES)

ENDBLOCK
ELSE 
BLOCK ! Global model
  INTEGER (KIND=JPIM), ALLOCATABLE :: NLOENG (:)
  REAL (KIND=JPRB) :: ZLONC, ZLATC

  ALLOCATE (NLOENG (YLCADR%NLATIT))

  IF (MODULO (YLCADR%NLATIT, 2) == 0) THEN
    NLOENG (1:YLCADR%NLATIT/2) = INT (YLCADR%NLOPAR (1:YLCADR%NLATIT/2), JPIM)
    NLOENG (YLCADR%NLATIT/2+1:YLCADR%NLATIT) = INT (NLOENG (YLCADR%NLATIT/2:1:-1), JPIM)
  ELSE
    CALL ABOR1 ('UNEXPECTED ODD NUMBER OF LATITUDES')
  ENDIF

  ZLATC = RAD2DEG * ASIN (YLCADR%SSLAPO)
  ZLONC = RAD2DEG * ATAN2 (YLCADR%SSLOPO, YLCADR%SCLOPO)

  YDGRID = ATLAS_REDUCEDGAUSSIANGRID (NLOENG, [ZLONC, ZLATC], YLCADR%SCODIL)

ENDBLOCK
ENDIF

CALL FAIRME (IREP, ILUN, 'KEEP')

END SUBROUTINE




END 

